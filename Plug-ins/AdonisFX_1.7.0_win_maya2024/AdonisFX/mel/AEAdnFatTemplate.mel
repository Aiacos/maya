global proc AEAdnFatMaterialsNew( string $attrName )
{
  // Create an option menu for materials.
  attrEnumOptionMenuGrp
    -l "Material"
    -annotation ("Defines the solver stiffness:" +
                 "\nFat 1e+3;\nMuscle 5e+3;\nRubber 1e+6;\nTendon 5e+7;" +
                 "\nLeather 1e+8;\nWood 6e+9;\nConcrete 2.5e+10;\nSkin 12e3.")
    -columnAlign 1 "right"
    -at $attrName
    AdnFatMaterialsEnumOptionMenu;
}

global proc AEAdnFatMaterialsReplace( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Disable de material option menu if the attribute is not present.
  if ( `attributeQuery -node $nodeName[0] -exists "material"` )
    attrEnumOptionMenuGrp -e -enable true -attribute $attrName AdnFatMaterialsEnumOptionMenu;
  else
    attrEnumOptionMenuGrp -e -enable false AdnFatMaterialsEnumOptionMenu;
}

global proc AEAdnFatStiffnessMultiplierNew( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Create a custom slider for the stiffness multiplier.
  attrFieldSliderGrp
    -l "Stiffness Multiplier"
    -annotation "Sets the scaling factor applied to the stiffness."
    -columnAlign 1 "right"
    -at $attrName
    -enable true
    AdnFatStiffnessMultiplierSliderGrp;
}

global proc AEAdnFatStiffnessMultiplierReplace( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Disable de material stiffness multiplier if the attribute is not present.
  if ( `attributeQuery -node $nodeName[0] -exists "stiffnessMultiplier"` )
    attrFieldSliderGrp -e -enable true -attribute $attrName AdnFatStiffnessMultiplierSliderGrp;
  else
    attrFieldSliderGrp -e -enable false AdnFatStiffnessMultiplierSliderGrp;
}

global proc AEAdnFatPointMassModeNew( string $attrName )
{
  // Create an option menu for the point mass mode.
  attrEnumOptionMenuGrp
    -l "Point Mass Mode"
    -annotation ("Defines how masses should be used in the solver:" +
                 "\nBy Density estimates the mass value by multiplying the Density * Volume." +
                 "\nBy Uniform Value sets a uniform mass value.")
    -columnAlign 1 "right"
    -at $attrName
    AdnFatPointMassModeEnumOptionMenu;
}

global proc AEAdnFatPointMassModeReplace( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Disable the point mass mode option menu if the attribute is not present.
  if ( `attributeQuery -node $nodeName[0] -exists "pointMassMode"` )
    attrEnumOptionMenuGrp -e -enable true -attribute $attrName AdnFatPointMassModeEnumOptionMenu;
  else
    attrEnumOptionMenuGrp -e -enable false AdnFatPointMassModeEnumOptionMenu;
}

global proc AEAdnFatPointMassChanged( string $nodeName )
{
  // Read the values
  string $attr = ($nodeName + ".pointMassMode");
  int $pointMassMode = `getAttr $attr`;

  // Enable/disable density slider
  if ( `attrFieldSliderGrp -exists "AdnFatDensitySliderGrp"` )
  {
    // NOTE: We have to change this entry if we ever reorder the pointMassMode menu
    $kByDensity = 0;
    attrFieldSliderGrp -e -enable ($pointMassMode == $kByDensity) AdnFatDensitySliderGrp;
  }
}

global proc AEAdnFatDensityNew( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Create a custom slider for the density.
  attrFieldSliderGrp
    -l "Density"
    -annotation ("Sets the density value in kg/m3 to be able to estimate mass values with By Density mode. " +
                 "The value is internally converted to g/cm3.")
    -columnAlign 1 "right"
    -at $attrName
    -enable true
    AdnFatDensitySliderGrp;
}

global proc AEAdnFatDensityReplace( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Disable de density if the attribute is not present.
  if ( `attributeQuery -node $nodeName[0] -exists "density"` )
    attrFieldSliderGrp -e -enable true -attribute $attrName AdnFatDensitySliderGrp;
  else
    attrFieldSliderGrp -e -enable false AdnFatDensitySliderGrp;
}

global proc AEAdnFatSolverStiffnessNew( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Create a custom slider for the stiffness.
  attrFieldSliderGrp
    -l "Stiffness"
    -annotation "Sets the custom stiffness value."
    -columnAlign 1 "right"
    -at $attrName
    -enable false
    AdnFatSolverStiffnessSliderGrp;
}

global proc AEAdnFatSolverStiffnessReplace( string $attrName )
{
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  // Disable de material stiffness if the attribute is not present.
  if ( `attributeQuery -node $nodeName[0] -exists "stiffness"` )
    attrFieldSliderGrp -e -enable true -attribute $attrName AdnFatSolverStiffnessSliderGrp;
  else
    attrFieldSliderGrp -e -enable false AdnFatSolverStiffnessSliderGrp;
}

global proc AEAdnFatDebugColorChanged(string $nodeName)
{
  // Callback method for the color widget on the attribute template.
  // Will update the deformer values based on the widget.
  float $rgbValues[];
  $rgbValues = `colorSliderGrp -q -rgb AdnFatDebugColorWidgetGrp`;

  setAttr ($nodeName + ".debugColorRed")   $rgbValues[0];
  setAttr ($nodeName + ".debugColorGreen") $rgbValues[1];
  setAttr ($nodeName + ".debugColorBlue")  $rgbValues[2];
}

global proc AEAdnFatDebugColorNew( string $attrName )
{
  // This method is called when the attribute editor is opened the first time.
  // Creates a color slider group.
  // Assigns callback method to color slider group and defaults to a red color.
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  string $changeCommand = ("AEAdnFatDebugColorChanged(\"" + $nodeName[0] + "\")");

  colorSliderGrp
    -l "Color"
    -annotation "Set the draw color for debug purposes."
    -rgb 1 0 0
    -cc $changeCommand
    AdnFatDebugColorWidgetGrp;
}

global proc AEAdnFatDebugColorReplace( string $attrName )
{
  // Method called everytime we change between deformers of the same type.
  // Will update the references to now update the new deformer.
  string $nodeName[];
  tokenize($attrName, ".", $nodeName);

  string $changeCommand = ("AEAdnFatDebugColorChanged(\"" + $nodeName[0] + "\")");
  float $component_r = `getAttr ($nodeName[0] + ".debugColorRed")`;
  float $component_g = `getAttr ($nodeName[0] + ".debugColorGreen")`;
  float $component_b = `getAttr ($nodeName[0] + ".debugColorBlue")`;

  colorSliderGrp
    -e
    -rgb $component_r $component_g $component_b
    -cc $changeCommand
    AdnFatDebugColorWidgetGrp;
}

global proc AEAdnFatHandleSolverStiffnessUI( string $nodeName )
{
  if ( `attributeQuery -node $nodeName -exists "useCustomStiffness"` )
  {
    $useCustomStiffness = `getAttr ($nodeName + ".useCustomStiffness")`;
    $showCustomStiffness = !$useCustomStiffness;

    // Disable the material stiffness option menu if the custom stiffness is activated.
    if ( `attrEnumOptionMenuGrp -exists "AdnFatMaterialsEnumOptionMenu"` )
      attrEnumOptionMenuGrp -e -enable $showCustomStiffness AdnFatMaterialsEnumOptionMenu;

    // Enable the stiffness multiplier if the custom stiffness is deactivated.
    if ( `attrFieldSliderGrp -exists "AdnFatStiffnessMultiplierSliderGrp"` )
      attrFieldSliderGrp -e -enable $showCustomStiffness AdnFatStiffnessMultiplierSliderGrp;

    // Enable the stiffness if the custom stiffness is activated.
    if ( `attrFieldSliderGrp -exists "AdnFatSolverStiffnessSliderGrp"` )
      attrFieldSliderGrp -e -enable $useCustomStiffness AdnFatSolverStiffnessSliderGrp;
  }
}

global proc AEAdnFatTemplate ( string $nodeName )
{
  editorTemplate -beginScrollLayout;

    // Solver Attributes section
    editorTemplate -beginLayout "Solver Attributes" -collapse 0;
      editorTemplate -label "Enable"
        -annotation "Flag to toggle the solver execution."
        -addControl "enable";
      editorTemplate -label "Substeps"
        -annotation "Number of solver substeps for one single time delta."
        -addControl "substeps";
      editorTemplate -label "Iterations"
        -annotation ("Sets the number of solver iterations." +
                     "\nHigher values produce more accurate results but may impact the performance.")
        -addControl "iterations";
      editorTemplate -callCustom
        "AEAdnFatMaterialsNew"
        "AEAdnFatMaterialsReplace"
        "material";
      editorTemplate -callCustom
        "AEAdnFatStiffnessMultiplierNew"
        "AEAdnFatStiffnessMultiplierReplace"
        "stiffnessMultiplier";
    editorTemplate -endLayout;

    editorTemplate -beginLayout "Time Attributes" -collapse 0;
      editorTemplate -label "Preroll Start Time"
        -annotation "Sets the frame at which the preroll begins. The preroll ends at Start Time."
        -addControl "prerollStartTime";
      editorTemplate -label "Start Time"
        -annotation "Determines the frame at which the simulation starts."
        -addControl "startTime";
      editorTemplate -label "Current Time"
        -annotation "Current frame of the simulation."
        -addControl "currentTime";
    editorTemplate -endLayout;

    editorTemplate -beginLayout "Scale Attributes";
      editorTemplate -label "Time Scale"
        -annotation "Sets the scaling factor applied to the simulation time step."
        -addControl "timeScale";
      editorTemplate -label "Space Scale"
        -annotation "Sets the scaling factor applied to the masses and/or the forces."
        -addControl "spaceScale";
      editorTemplate -label "Space Scale Mode"
        -annotation "Determines if the spatial scaling affects the masses, the forces, or both."
        -addControl "spaceScaleMode";
    editorTemplate -endLayout;

    editorTemplate -beginLayout "Gravity";
      editorTemplate -label "Gravity"
        -annotation "Sets the magnitude in m/s2 of the gravity acceleration. The value is internally converted to cm/s2."
        -addControl "gravity";
      editorTemplate -label "Gravity Direction"
        -annotation "Sets the direction of the gravity acceleration."
        -addControl "gravityDirection";
    editorTemplate -endLayout;

    // Advanced Settings section
    editorTemplate -beginLayout "Advanced Settings" -collapse 1;
      // Advanced Initialization Settings subsection
      editorTemplate -beginLayout "Initialization Settings" -collapse 1;
        editorTemplate -label "Hard At Start Time"
          -annotation ("Initialize Hard Constraints on start time.")
          -interruptOptimize
          -addControl "initHardAtStartTime";
        editorTemplate -label "Shape Preservation At Start Time"
          -annotation ("Initialize Shape Preservation Constraints on start time.")
          -interruptOptimize
          -addControl "initShapePreservationAtStartTime";
      editorTemplate -endLayout;
      // Advanced Stiffness Parameters subsection
      editorTemplate -beginLayout "Stiffness Settings" -collapse 1;
        editorTemplate -label "Use Custom Stiffness"
          -annotation ("Toggles the use of a custom stiffness value." +
                       "\nIf enabled, the Material stiffness value is ignored and the " +
                       "Stiffness attribute below is used instead.")
          -addControl "useCustomStiffness" "AEAdnFatHandleSolverStiffnessUI";
        editorTemplate -callCustom
          "AEAdnFatSolverStiffnessNew"
          "AEAdnFatSolverStiffnessReplace"
          "stiffness";
      editorTemplate -endLayout;
      // Advanced Override Stiffness subsection
      editorTemplate -beginLayout "Override Constraint Stiffness" -collapse 1;
        editorTemplate -label "Solver Stiffness"
          -annotation "Global stiffness currently used by the solver."
          -addControl "outputSolverStiffness";
        editorTemplate -label "Hard Constraints"
          -annotation "If its value is equal or greater than 0.0, it will override the global stiffness for hard constraints."
          -addControl "hardStiffnessOverride";
        editorTemplate -label "Vol Shape Preservation"
          -annotation "If its value is equal or greater than 0.0, it will override the global stiffness for volume shape preservation constraints."
          -addControl "volumeShapeStiffnessOverride";
        editorTemplate -label "Shape Preservation"
          -annotation "If its value is equal or greater than 0.0, it will override the global stiffness for shape preservation constraints."
          -addControl "shapeStiffnessOverride";
      editorTemplate -endLayout;
      editorTemplate -beginLayout "Mass Properties" -collapse 1;
        // Advanced Mass Properties subsection
        editorTemplate
          -addControl "pointMassMode" "AEAdnFatPointMassChanged"
          -suppress
          -callCustom "AEAdnFatPointMassModeNew" "AEAdnFatPointMassModeReplace" "pointMassMode";
        editorTemplate -callCustom
          "AEAdnFatDensityNew"
          "AEAdnFatDensityReplace"
          "density";
        editorTemplate -label "Global Mass Multiplier"
          -annotation "Sets the scaling factor applied to the mass of every point."
          -addControl "globalMassMultiplier";
      editorTemplate -endLayout;
      editorTemplate -beginLayout "Dynamic Properties" -collapse 1;
        editorTemplate -label "Global Damp. Multiplier"
          -annotation "Sets the scaling factor applied to the global damping of every point."
          -addControl "globalDampingMultiplier";
        editorTemplate -label "Inertia Damper"
          -annotation "Sets the linear damping applied to the dynamics of every point.\nRange 0 to 1."
          -addControl "inertiaDamper";
        editorTemplate -label "Attenuation Vel. Factor"
          -annotation ("Sets the attenuation factor that drives how much velocity attenuation is" +
                       "\napplied based on a matrix provided to the plug 'AttenuationMatrix'.\nRange 0 to 1.")
          -addControl "attenuationVelocityFactor";
      editorTemplate -endLayout;
      editorTemplate -beginLayout "Volume Structure" -collapse 1;
        // Advanced Volume Structure subsection
        editorTemplate -label "Divisions"
        -annotation "Number of internal subdivisions between the base mesh and simulated mesh."
        -addControl "divisions";
      editorTemplate -endLayout;
    editorTemplate -endLayout;

    // Deformer Attributes section
    editorTemplate -beginLayout "Deformer Attributes" -collapse 1;
      editorTemplate -label "Envelope" -addControl "envelope";
    editorTemplate -endLayout;

    // Debug section
    editorTemplate -beginLayout "Debug" -collapse 1;
      editorTemplate -label "Debug"
        -annotation "Toggles visualization of debugger."
        -addControl "debug";
      editorTemplate -label "Feature"
        -annotation "Sets the feature to debug."
        -addControl "debugFeature";
      editorTemplate -label "Width Scale"
        -annotation "Sets the scaling factor applied to the width of the debug lines."
        -addControl "debugWidth";
      editorTemplate -callCustom
        "AEAdnFatDebugColorNew"
        "AEAdnFatDebugColorReplace"
        "debugColor";
    editorTemplate -endLayout;

  // suppressed attributes

  editorTemplate -addExtraControls;
  editorTemplate -endScrollLayout;
}
